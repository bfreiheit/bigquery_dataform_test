config {
    type: "table",
    schema: "analytics_marts",
    description: "Aggregated sessions",
    columns: {
        days_to_contract_start: "Days between lat session date and contract start date",
        users: "Unique user count",
        conversions: "Converted sessions"
    },
    assertions: {
      nonNull: ["session_date"]
    },   
    tags: ["marts"]
}

WITH
  journey_lengths AS (
  SELECT
    user_id,
    COUNT(DISTINCT session_id) AS touchpoints
  FROM
    ${ref('merged_sessions')}
  GROUP BY
    user_id )
SELECT
  session_date,
  sl.channel_type,
  bike_type,
  bike_brand,
  COUNT(session_id) sessions,
  COUNT(DISTINCT user_id) users,
  COUNTIF(DISTINCT account_id IS NOT NULL) conversions,
  CASE
    WHEN account_id IS NOT NULL THEN DATE_DIFF(contract_start_date, session_date, DAY)
    ELSE 0
END
  days_to_contract_start,
  SUM(saleprice_gross) total_revenue,
  jl.touchpoints
FROM
  ${ref("merged_sessions")} AS sl
JOIN
  journey_lengths jl
USING
  (user_id)
GROUP BY
  session_date,
  contract_start_date,
  channel_type,
  account_id,
  bike_type,
  bike_brand,
  touchpoints
